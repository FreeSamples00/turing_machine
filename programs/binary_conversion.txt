# ==================== README ====================
#
#  '#' indicate comments
#
# Symbols:
#  READ SYMBOL: '_' indicates a blank, '*' indicates a wild card
#  WRITE SYMBOL: '_' indicates a deletetion, '*' indicates no change
#  MOVEMENT: 'r' move right, 'l' move left, '*' do not move
#  NEW STATE: '*' do not change state
#
# Instruction example: (space separated)
#  <current_state> <input> <write_symbol> <move_direction> <new_state>
#
# Built in states:
#  'halt-accept': halts processing, input accepted
#  'halt-reject': halts processing, input rejected
#  'halt': halts processing
#  'start': machine is initialized with this state, must define how to start program
# ============== PROGRAM DESCRIPTION ==============
# 
#  This program converts binary numbers to decimal.
#
#  Program was found here: https://morphett.info/turing/turing.html
#
# ===================== INPUT =====================
#
#  '<binary_number>'
#
# ================= BEGIN PROGRAM =================

# starting state
start * * * 1

1 _ _ r 1
1 * * r 1a
1a * * r 1a
1a _ _ l 2   # found the end of the input
1b _ _ r 1   # (used later) skip the output, go to end of input
1b * * r 1b
2 1 0 l 3    # decrement input
2 0 1 l 2    # decrement & carry
2 _ _ r 20   # finished. clean up
3 * * l 3    # find start of output
3 _ _ l 4    # found end of input
4 0 1 r 1b   # increment the output
4 1 2 r 1b
4 2 3 r 1b
4 3 4 r 1b
4 4 5 r 1b
4 5 6 r 1b
4 6 7 r 1b
4 7 8 r 1b
4 8 9 r 1b
4 9 0 l 4  # increment & carry
4 _ 1 r 1b  # start new column

# clean up
20 _ _ l 21  # return to end of output
20 * _ r 20  # erase input
21 _ _ l 21
21 * * l 21a
21a * * l 21a
21a _ _ r halt


